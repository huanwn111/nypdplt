                                                           临床试验中随机化分组及其SAS实现


随机对照试验（randomized controlled trial, RCT）对某种疗法或药物的效果进行检测的手段，常用于医学，生物学，农学。随机对照试验的基本方法是，将研究对象随机分组，对不同组实施不同的干预，以对照效果的不同。能够最大程度地避免临床试验设计、实施中的偏倚，平衡混杂因素，提高统计学检验的有效性等作用，被公认为是评价干预措施的金标准。

RCT需要对研究对象进行随机化分组，技术上可以通SAS生成随机化表。

随机分组的分方分类：
简单随机化（simple randomization）
分区组随机化（block randomization）
分层随机化（stratified randomization）
分层分区组随机化（stratified block randomization）
动态随机化（dynamic randomization）

一、简单随机化，又称完成随机化

定义：将研究对象进行直接随机抽取分组，不作何限制与干预调整。可以通过随机数字表、计算机产生随机数、掷硬币、或函数等方法进行随机化。
适用于：研究例数少，个体差异较小。
缺点：组间病例特征可能出现不平衡。
策略：随机数据表法、随机数余数组法、SAS函数实现。

例1-1（随机数表法）：两组
对心脑病区观察20例(编号1～20)心血管病患者分为2组，一组以灯盏花注射液为治疗组，另一组给予瓜蒌薤白汤。
从随机数字表任一行开始，按序查看，凡小于或等于20的数标记，把标记的数字相应序号的患者挑出来，查够10个数为一组，其余序号患者为另一组。

第一组：9，10，4，6，15，20，11，12，3，7；第二组：1， 2，5，8， 13，14，16，17，18，19。
SAS代码：
/*生成随机表proc plan seed=1000*/                                                                                                                          
proc plan seed=1000;                                                                                                                    
        factors n=20;                                                                                                                   
        output out =randnum;/*output遍历输出每一个存到数据集中*/                                                                                                            
run;
/*分组*/
data result;                                                                                                                            
        set randnum;/*复制随机数表*/                                                                                                      
        index =_n_;/*建序号列，为递增数字*/                                                                                             
        if n<=10 then group='A';/*随机数表n小于10的划到A组，其它为B组*/                                                                   
        else group = 'B';                                                                                                               
run;  

/*打印*/                                                                                                                                  
proc print data=result noobs; /*noobs不带打印自带的序号*/                                                                                                          
        var index group;/*选择序号列和分组列打印*/                                                                                                                
run;                                                                                                                                             
方法二：
/*uniform生成20个随机数*/                                                                                                               
data randnum2;                                                                                                                          
        do index =1 to 20;                                                                                                              
                r=uniform(210000);                                                                                                      
                output;                                                                                                                 
        end;                                                                                                                            
run;                                                                                                                                    
                                                                                                                                        
/*为randum2的r列排序，r列为小数，生成排名变量1-20的整数，作为随机数，此处也可通过r*20取整*/                                             
proc rank data=randnum2 out=rank;                                                                                                       
        ranks r_rank;                                                                                                                   
        var r;                                                                                                                          
run;                                                                                                                                    
/*分组*/                                                                                                                                
data result2;                                                                                                                           
        set rank;                                                                                                                       
        if r_rank<=10 then group='A';                                                                                                   
        else group='B';                                                                                                                 
RUN;
例1-2（随机数余数分组法）：多组(≥3组)
将15名血栓性血瘀证患者分为3组。
随机数余数分组法操作方法：
编号：研究对角从1到N编号；
获取随机数：从随机数字表任意位置开始，每个对象顺序取一个随机数字；
求余：用各自拿到的随机数与组数求余，作为分组号，若带队则刚好是组数。（可用奇偶单双号理解本段）
分组：按上面求得的分组号分组；
调整：有例数不均时，需调整的，则再用随机数余数法抽取这一组，不看其它，只看这一组，随机表取一个随机数，与本组例数求余，余数是第几就是第几个对象被抽中换到其它组。




SAS代码：

/*uniform生成20个随机数*/                                                                                                               
data randnum2;                                                                                                                          
        do index =1 to 20;                                                                                                              
                r=uniform(210000);                                                                                                      
                output;                                                                                                                 
        end;                                                                                                                            
run;                                                                                                                                    
                                                                                                                                        
/*为randum2的r列排序，r列为小数，生成排名变量1-20的整数，作为随机数，此处也可通过r*20取整*/                                             
proc rank data=randnum2 out=rank;                                                                                                       
        ranks r_rank;                                                                                                                   
        var r;                                                                                                                          
run;
/*随机求余分组法分三组*/                                                                                                                
data result3;                                                                                                                           
        set rank;                                                                                                                       
        if mod(r_rank,3)=1 then group='甲';/*mod求余*/                                                                                             
        else if mod(r_rank,3)=2 then group='乙';                                                                                        
        else group='丙';                                                                                                                
run;     

二、分区组随机化，又称均衡随机化、限制性随机化
定义：将随机加以约束，干预，使其相对平衡，满足研究要求，在同一区组内控制预定的分组数和组内研究对象比例。分区依据如：批号相同的试剂、体重相近的受试者等。
优点：避免了简单随机化分组产生的不平衡问题，提高统计效率，分配率不存在时间趋势积累的影响。
适用条件：区组长度（对象数）不宜太小，太小形成不随机。一般区组长度至少为组数的2位以上，如：3个组，每组要至少6人以上，2个组（试验组和对照组），区组长度4-8。
区组长度也不宜太大，太大易使用组内特征有差异，出现不均衡。
区组长度与试验疗程长短有关：短疗长的患者变动可能大，区组长度不宜过大。疗程短的区组长短不影响，患者入组快，结束快，变动可能小，入组后补容易。

例2-1 区组随机化分组（两组）
以入院时间(月份)作为配伍因素,将入院时间同月相邻的4位患者作为一个区组，试对24名患者分配到A和B两组处理。
分析：4个一组，分6组
SAS代码：
/*做一个6组*4人的双重循环，uniform生成一个区组内随机数表*/                                                                              
data randnum3;                                                                                                                          
        /*双重循环*/                                                                                                                    
        do block = 1 to 6;/*遍历6组*/                                                                                                   
                do length = 1 to 4;/*组内每个病人一个随机数*/                                                                           
                        r= uniform(210000);/*生成随机数表*/                                                                             
                        output;                                                                                                         
                end;                                                                                                                    
        end;                                                                                                                            
                                                                                                                                        
                                                                                                                                        
run;                                                                                                                                    
                                                                                                                                        
/*为随机数表加自增序号number列*/                                                                                                        
data randum4;                                                                                                                           
        set randnum3;                                                                                                                   
        number = _n_;                                                                                                                   
run;                                                                                                                                    
                                                                                                                                        
/*为随机数表加排序列*/                                                                                                                  
proc rank data=randum4 out=rank4;/*proc rank排序 out=输出表名 desending降序 ties=means按均值排序*/                                      
        ranks r_rank;/*生成的序号列*/                                                                                                   
        var r;/*排r列*/                                                                                                                 
        by block;/*分组，组内排序*/                                                                                                     
run;                                                                                                                                    
                                                                                                                                        
/*分组，按上面的序号组内分组，4人一组，<=2号的为A组，其它B组*/                                                                          
                                                                                                                                        
data result4;                                                                                                                           
        set rank4;                                                                                                                      
        if r_rank <=2 then group ='A';                                                                                                  
        else group = 'B';                                                                                                               
run;


三、分层随机化分组
定义：根据研究对象进入试验时的某些临床特征或危险因素分层，如年龄、性别、病情、疾病分期等；在每一层进行简单随机抽样分组，再分别合成试验组和对照组。
优点：可保证减小I类（I类弃真，II类存伪，因为抽样精确最大化利用了样本）错误，提高小样本（<400）试验的把握度。
适用条件：适合于2-3个分层因素，分层因素较多容易出现不均衡（因素多则层内例数少，所以不够量，不够有说明力，影响均衡性，影响分析结果）。
通常受试对象在100-200例之间，2-3个分层因素，每个因素仅有2个水平时，使用本法较恰当。
//水平：一个因素的不同情况称为该因素的水平。例如：灯光作为因素，则强光和弱光作为水平

例3-1 分层随机化分组（两组）
将男、女各10名受试者按照性别分层后随机等分为两组。
SAS代码：
同上，双重循环生成随机，分组加名字序号，判断序号一分为2分两组


四、分层分区组随机化分组
定义：多中心临床试验中，以中心分层，各中心内进行区组随机化，是一种分层分区组随机化。
目前临床试验中最多采用的是多中心（多位研究者按同一试验方案在不同地点和单位同时进行临床试验）、随机、对照（病例组和对照组）、平行（相同样品多取同步试验，比对一致性）、双盲试验（研究对象和研究者不知分组情况）。
优点：分层的随机保证了层间均衡性，区组的随机保证了组间的可比性。
适用条件：同样的，在影响因素（分层因素<3）比较少时，分层区组随机可以保证组间均衡性。而分层较多时则各层例数变少，易层出现分布和例数不均。

例：对240例病人，按4个中心进行区组随机化分组。
SAS代码：
方法一：
PROC PLAN SEED=210000;
     FACTORS center=4  block=10  length=6;/*4中心，10区组，每组6人，4*10*6=240 三层循环用空格间隔*/
     OUTPUT  OUT=randnum5;/*输出表名randnum5*/
RUN;
方法二：
/*做一个4中心*10组*6人的三重循环，uniform生成一个区组内随机数表*/                                                                       
data randnum6;                                                                                                                          
        /*三重循环*/                                                                                                                    
    do center = 1 to 4;                                                                                                                 
        do block = 1 to 10;/*遍历10组*/                                                                                                 
                do length = 1 to 6;/*组内6人每个病人一个随机数*/                                                                        
                        r= uniform(210000);/*生成随机数表*/                                                                             
                        output;                                                                                                         
                end;                                                                                                                    
        end;                                                                                                                            
     end;                                                                                                                               
                                                                                                                                        
run;                                                                                                                                    
                                                                                                                                        
/*为随机数表加自增序号number列*/                                                                                                        
data randnum7;                                                                                                                          
        set randnum6;                                                                                                                   
        number = _n_;                                                                                                                   
run;                                                                                                                                    
                                                                                                                                        
/*为随机数表加排序列*/                                                                                                                  
proc rank data=randnum7 out=rank7;/*proc rank排序*/                                                                                     
        ranks r_rank;/*生成的序号列*/                                                                                                   
        var r;/*排r列*/                                                                                                                 
        by center block;/*按中心 分层 分组，组内排序*/                                                                                  
run;                                                                                                                                    
                                                                                                                                        
/*分组，按上面的序号组内分组，6人一组，<=3号的为A组，其它B组*/                                                                          
                                                                                                                                        
data result7;                                                                                                                           
        set rank7;                                                                                                                      
        if r_rank <=3 then group ='A';                                                                                                  
        else group = 'B';                                                                                                               
run;

五、动态随机化分组
定义：调整分配，在试验过程中，每例患者分到各组的概率不是固定不变的，而是要根据一定条件进行调整的方法。
动态随机化包括：瓮法(urn)、偏币法(biased coin)、最小化法(minimization)等。
按平衡指数最小原则进行随机化分组，即动态随机化分组。
优点：保证各试验组间例数与某些重要的非处理因素（来源、性别、体重等次要因素）接近一致，即样本特征均衡。
国外，最小化法已用于实际研究，被誉为临床试验的“白金标准”。


六、随机化与盲法
为了避免偏倚，盲法和随机化要结合使用。

盲法：
单盲，研究对象不知分组情况；
双盲，研究对象和和研究者不知分组情况，一般采用这个；
三盲，研究对象、研究者、评价者都不知分组情况；

随机分配方案的隐匿，又称隐匿分组：
产生随机分配序列的人和确定受试对象合格的人不应是同一人；
产生随机分配序列和保存分配序列的人最好不是参与试验的人；
按顺序编号、不透光、密封的信封，或无碳写纸；
中心随机系统（未来的趋势）；
编号或编码的瓶子或容器；
中心药房来准备药物；

分组的隐匿与盲法的区别：
随机分配方案的隐匿，是避免选择性偏倚(selection bias)，保证试验组和对照组基线平衡；
盲法，分组后实施，为了避免实施偏倚(performance bias)和测量偏倚(detection bias)，因此，双盲也不能代替隐匿分组

七、临床试验方案中的随机化

临床随机对照试验（RCT）的设计方案和结果报告必须写明以下几点：
采用什么随机方法，由谁产生随机序列；
由谁确定随机序列的分组；
确定随机分配表的人是否参与纳入受试者；
随机分配表由谁保管及其方法；

随机分配表通常一式三份或四份，严密保存，使用不透光信封或容器封存；
由课题负责人、研究主办者、药房、统计师各保管一份；
揭盲或破盲时几份随机表必须同时当面揭封；
如有破损，则须说明，否则宣布分组信息泄密，甚至导致试验无效；
中央随机化（一个独立的机构或组织来安排和实施）并有严格的分组保密措施可以认为隐匿分组充分；
进行随机化RTC前，需要认真考虑伦理问题；

